;=======================================================================
; Program-15: one motor, position (backlash from one side only)
; PMAC-2 version
; (for Ram6X2 -- GM/CA CAT)
;
;----------------------------------------;
; $(assy)AccelTime  -> TA0   -> M(Q71)   ;
; $(assy)AccelCurve -> TS0   -> M(Q72)   ;
; $(m1)RqsPos       -> X0    -> M(Q73)   ;
; $(m1)RqsBkl       -> X1    -> M(Q74)   ;
; $(m1)RqsPosFra    -> X2    -> M(Q75)   ; == 0 unless encoder provides fractions of a count
;----------------------------------------;
;        PMAC2---v
; Use: Q70*100+5047,  Q71, Q72,  Q73,  Q74,  Q75
;       ${m1}DesPos   TA0, TS0,   X0,   X1,   X2
; Q70    is the coordinate system number.
; DesPos is nearly the same as ActPos - this is the position
;        around which PMAC is currently trying to hold the motor.
;
CLOSE
;
OPEN PROG 15 CLEAR
  TA(M(Q71+0))	;set acceleration time
  TS(M(Q72+0))	;set s-curve part of acceleration time
  FRAX(X)	;specify feedrate axis
  F0		;set feedrate (speed)  -- is not used here
  LINEAR
  ABS
  Q1=M(Q73+0)+M(Q75+0)			;RqsPos
  Q4=M(Q74+0)				;RqsBkl
  Q7=Q1-M(Q70*100+5047)			;RqsPos-DesPos
  IF(Q4>0 AND Q7<0  OR  Q4<0 AND Q7>0)
    A(Q1-Q4)				;goto RqsPos-RqsBkl
    DWELL0
    A(Q1)				;goto RqsPos
  ELSE
    A(Q1)				;goto RqsPos
  ENDIF
  DWELL0
; DELAY 100
CLOSE

